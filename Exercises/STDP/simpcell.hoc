/* Template of a single cell containing soma and dendrite
   BPG 23-3-09
*/

begintemplate SimpCell

public soma, dend, synE, synI, nc
public connect2target

create soma, dend

proc init() {

  create soma, dend

  soma {
    nseg=1
    L=30
    diam=30
    insert hh
  }

  dend {
    nseg=11
    L=1000
    diam=2
    insert pas
    g_pas=0.0001
    e_pas=-65
  }

  connect dend(0), soma(1)
  
  synapses()
}


objref nc
proc connect2target() { //$o1 target point process, $2 delay, $3 weight
  soma nc = new NetCon(&v(1), $o1)
  nc.delay = $2
  nc.weight = $3
  nc.threshold = -20
}


objref synE, synI
proc synapses() {
  /* E0 */   dend synE = new ExpSyn(0.8)	// excitatory synapse
    synE.tau = 2
    synE.e = 0
  /* I1 */   dend synI = new ExpSyn(0.1)	// inhibitory synapse
    synI.tau = 50
    synI.e = -80
}

endtemplate SimpCell

